# Plugins {{{ #

export ZPLUG_HOME=/usr/local/opt/zplug
source $ZPLUG_HOME/init.zsh

# completions
zplug "zsh-users/zsh-completions"

zplug "zsh-users/zsh-syntax-highlighting", defer:2
zplug "zsh-users/zsh-history-substring-search", defer:3
zplug "zsh-users/zsh-autosuggestions"

zplug "plugins/docker-compose", from:oh-my-zsh
zplug "plugins/docker", from:oh-my-zsh

zplug "morhetz/gruvbox", use:"gruvbox_256palette.sh"
zplug "russtone/prompt-russtone", as:theme, use:theme.zsh

zplug load

# }}} Plugins #

# Completions {{{ #

zmodload zsh/complist

setopt AUTO_MENU
setopt COMPLETE_IN_WORD
setopt ALWAYS_TO_END

# Case insensetive completion
zstyle ':completion:*' matcher-list \
  'm:{[:lower:]}={[:upper:]}'\
  'm:{[:upper:]}={[:lower:]}'

# Use cache
zstyle ':completion::complete:*' use-cache on

# Use menu if more than one result
zstyle ':completion:*:*:*:*:*' menu select

# Format of group name
zstyle ':completion:*' format ' %F{yellow}-- %d --%f'

# Colors in completion menu
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# }}} Completions #

# Key bindings {{{ #

bindkey '^p' history-substring-search-up
bindkey '^n' history-substring-search-down

bindkey '^u' backward-kill-line
bindkey '^t' kill-line

bindkey '^f' vi-forward-char
bindkey '^b' vi-backward-char

bindkey '^a' beginning-of-line
bindkey '^e' end-of-line

bindkey '^d' delete-char-or-list

# Use vim keys in tab complete menu:
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# Fix backspace after mode switching
bindkey -v '^?' backward-delete-char

# Edit current command in nvim with Ctrl-R
autoload edit-command-line
zle -N edit-command-line
bindkey '^g' edit-command-line

# }}} Key bindings #

# Cursor {{{ #

# Change cursor shape for different vi modes.
function zle-keymap-select {
    if [[ ${KEYMAP} == vicmd ]]; then
        echo -ne '\e[1 q'
    else
        echo -ne '\e[5 q'
    fi
}
zle -N zle-keymap-select

function zle-line-init {
    # initiate `vi insert` as keymap (can be removed if `bindkey -V` has been set elsewhere)
    zle -K viins
    echo -ne '\e[5 q'
}
zle -N zle-line-init

# Use beam shape cursor on startup.
echo -ne '\e[5 q'

# Use beam shape cursor for each new prompt.
function preexec {
    echo -ne '\e[5 q'
}

# }}} #

# Dircolors {{{ #

if (( $+commands[gdircolors] )); then
  dircolors_cmd='gdircolors'
elif (( $+commands[dircolors] )); then
  dircolors_cmd='dircolors'
fi

if [[ ! -z $dircolors_cmd ]]; then
  eval "$($dircolors_cmd --sh "$HOME/.dircolors")"
fi

if (( $+commands[gls] )); then
  ls_cmd='gls'
else
  ls_cmd='ls'
fi
alias ls="$ls_cmd --color=auto --group-directories-first"

# }}} Dircolors #

# Aliases {{{ #

alias p='python3'
alias pp='ptpython'
alias v='nvim'
alias hex='open -a Synalyze\ It\!\ Pro'
alias now='date +'%d-%m-%Y_%H:%M''
alias w='watch -n1'

# sed
if (( $+commands[gsed] )); then
    alias sed=gsed
fi

# }}} Aliases #

# History {{{ #

# Variables
HISTFILE=$HOME/.zsh_history
HISTSIZE=100000
SAVEHIST=100000

# Options
setopt SHARE_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_SAVE_NO_DUPS
setopt HIST_VERIFY
setopt INC_APPEND_HISTORY

# }}} History #

# FZF {{{

if (( $+commands[fzf] )); then

  # Color scheme
  export FZF_DEFAULT_OPTS='
   --color fg:15,bg:0,hl:3,fg+:3,bg+:0,hl+:3
   --color info:7,prompt:2,spinner:3,pointer:4,marker:3
  '

  # Use ag if possible
  if (( $+commands[ag] )); then
    export FZF_DEFAULT_COMMAND='ag -g ""'
  fi

fi

# }}}

# Tools completions {{{ #

# Completions from files
compfiles=(
    # gcloud
    /usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc
)

for f in "${compfiles[@]}"; do
    if [ -f $f ]; then
        source $f
    fi
done

# Completions from commands
compcmds=(
    oc
    kubectl
)

for c in "${compcmds[@]}"; do
    if (( $+commands[$c] )); then
        source <(kubectl completion zsh)
    fi
    # Workaround
    # https://github.com/kubernetes/kubectl/issues/125
    clear
done

# }}} Tools completions #

# vim: fdm=marker ft=zsh
